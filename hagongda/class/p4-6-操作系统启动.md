# 操作系统启动
## 从白纸到图灵机

![](./images/tulingji00.png)

## 从图灵机到通用图灵机
![](./images/tongyongtulingji.png)

## 从通用图灵机到计算机
![](./images/jisuanji.png)

## 打开电源，计算机执行的第一句指令什么?

![](./images/ipaddress.png)

## bootsect.s

`bootsect.s` 是磁盘引导块程序，驻留在磁盘的第一个扇区中(引导扇区，0 磁道(柱面)，0 磁头，第一个扇区)。在 PC 加电 ROM BIOS 自检后，ROM BIOS 会把引导扇区代码 bootsect 加载到内存地址 0x7c00 开始处并执行。

![](./images/bootsectmem.png)


### **0x01 移动 bootsect**

```
SETUPLEN = 4				! nr of setup-sectors
BOOTSEG  = 0x07c0			! original address of boot-sector
INITSEG  = 0x9000			! we move boot here - out of the way
SETUPSEG = 0x9020			! setup starts here
SYSSEG   = 0x1000			! system loaded at 0x10000 (65536).
ENDSEG   = SYSSEG + SYSSIZE	! where to stop loading

entry _start
_start:
	mov	ax,#BOOTSEG     ! 将 ds 的段寄存器设置为 0x07c0
	mov	ds,ax
	mov	ax,#INITSEG     ! 将 es 的段寄存器设置为 0x9000
	mov	es,ax
	mov	cx,#256         ! 设置移动字数值为 256字
	sub	si,si           ! 源地址为: ds:si = 0x07c0:0x0000
	sub	di,di           ! 目的址为: es:di = 0x9000:0x0000
	rep                 ! 重复执行并递减 cx 的值，直到 cx = 0 为止
	movw                ! 即 movs 指令，从内存 [si] 处，移动 256 字到内存 [di] 处。
	jmpi	go,INITSEG  ! 段间跳转(Jump Intersegment); 这里的 INITSEG 指要跳到的段地址，
                        ! 标号 go 是偏移地址
```

这里注意：段地址是要左移 4 位的即：

源地址为: 0x07c0 << 4 + 0x0000
目的址为: 0x9000 << 4 + 0x0000

上面的代码做了将 `bootsect.s` 代码从 `0x07c0` 移动到 `0x90000` 处，如下图：

![](./images/movbootsect.png)


### **0x02 读取 setup**
这里先介绍一下 `INT 13` 中断

```
入口参数：
（ah）= int 13H 的功能号（2表示读扇区、3表示写扇区）
（al）= 读取/写入的扇区数
（ch）= 磁道号
（cl）= 扇区号
（dh）= 磁头号（对于软盘即面号，对软盘一个面用一个磁头来读写）
（dl）= 驱动器号  软驱从0开始，0：软驱A 、 1：软驱B
                    硬盘从80H开始，80H：硬盘C 、 81H： 硬盘D
                    
es:bx 指向接收从扇区读入数据的内存区/指向将写入磁盘的数据


返回参数：
操作成功，（ah）= 0 ，（al）= 读取/写入的扇区数
操作失败，（ah）= 出错代码
```

```arm
! 从下面开始，CPU 已经移动到 0x90000 位置处的代码中执行
! 其中设置的堆栈寄存器 ss 和 sp，sp 需要指向远大于 512 字节偏移处(即 0x90200),
! 因为 0x90200 开始处还要放置 steup 程序，此时的 setup 程序大约 4 个扇区，因此，
! sp 要指向远大于 (0x200 + 0x200 * 4 + 堆栈大小)处。
go:	mov	ax,cs ! 这里的 cs = 0x9000
	mov	ds,ax ! ds = 0x9000
	mov	es,ax ! es = 0x9000
! put stack at 0x9ff00. 
! 将堆栈指针 sp 指向地址 0x9ff00，即(0x9000:0xff00)
	mov	ss,ax ! ss = 0x9000
	mov	sp,#0xFF00		! arbitrary value >>512

! load the setup-sectors directly after the bootblock.
! Note that 'es' is already set up.

! 下面开始利用 BIOS 的中断 INT 13，将 setup 程序从磁盘的第二扇区
! 开始读到 0x90200 处，工读 4 个扇区。
! 如果读错，则复位驱动器并重试，没有退路。
! INT 13 中断的使用方法如下：
! 读扇区：
! ah = 0x02（读磁盘扇区到内存） al = 需要读出的扇区数量
! ch = 磁道(柱面)              cl = 开始扇区(位 0 ~ 5)，磁道号高 2 位(位 6 ~ 7)
! dh = 磁头号                  dl = 驱动器号(如果是硬盘则位 7 要置位)
! es:bx 指向数据缓冲区
load_setup:
	mov	dx,#0x0000		! drive 0, head 0
	mov	cx,#0x0002		! sector 2, track 0
	mov	bx,#0x0200		! address = 512, in INITSEG（设置将内容读到的地址）
	mov	ax,#0x0200+SETUPLEN	! service 2, nr of sectors
	int	0x13			! read it
	jnc	ok_load_setup		! ok - continue
	mov	dx,#0x0000
	mov	ax,#0x0000		! reset the diskette
	int	0x13
	j	load_setup
```
这里先看一下 INT 13 读取驱动器参数介绍：

```
功能08H  
功能描述：读取驱动器参数 
入口参数：AH＝08H  DL＝驱动器，00H~7FH：软盘；80H~0FFH：硬盘 

出口参数：CF＝1——操作失败，AH＝状态代码，参见功能号01H中的说明，否则， 
BL＝01H — 360K 
  ＝02H — 1.2M 
  ＝03H — 720K 
  ＝04H — 1.44M 

CH＝柱面数的低8位 CL的位7-6＝柱面数的该2位 CL的位5-0＝扇区数 

DH＝磁头数 DL＝驱动器数 
ES:DI＝磁盘驱动器参数表地址 
```
开始读取

```arm
ok_load_setup:

! Get disk drive parameters, specifically nr of sectors/track

	mov	dl,#0x00
	mov	ax,#0x0800		! AH=8 is get drive parameters
	int	0x13
	mov	ch,#0x00 ! 读到的结果会存在 cx 中
	seg cs
	mov	sectors,cx
	mov	ax,#INITSEG
	mov	es,ax     ! 因为上面取磁盘参数中断改动了 es 的值，这里恢复
```

**关于 `seg cs` 这里说明一下**：

`seg cs` 这样的语句只影响到它下一条指令 `mov sectors,cx`

两句合起来等价于: `mov cs:[sectors],ax`, 也即将 `ax` 中的内容存储到地址： `cs:[sectors]`

[seg cs是什么意思，看看这里就知道了！](https://blog.csdn.net/robbie1314/article/details/6308055)

### **0x03 打印 load system**

```arm
! Print some inane message

	mov	ah,#0x03		! read cursor pos
	xor	bh,bh
	int	0x10
	
	mov	cx,#24
	mov	bx,#0x0007		! page 0, attribute 7 (normal)
	mov	bp,#msg1
	mov	ax,#0x1301		! write string, move cursor
	int	0x10
```

### **0x04 读取 system 代码**

```arm
! Ok, we have written the message, now
! we want to load the system (at 0x10000)
	mov	ax,#SYSSEG
	mov	es,ax		! segment of 0x010000
	call	read_it
	call	kill_motor
```



## 参考
- [linux源码阅读笔记 jmpi指令](https://luogantt.blog.csdn.net/article/details/126836483?spm=1001.2101.3001.6650.10&utm_medium=distribute.pc_relevant.none-task-blog-2%7Edefault%7EBlogCommendFromBaidu%7ERate-11-126836483-blog-5735747.pc_relevant_default&depth_1-utm_source=distribute.pc_relevant.none-task-blog-2%7Edefault%7EBlogCommendFromBaidu%7ERate-11-126836483-blog-5735747.pc_relevant_default&utm_relevant_index=11)
- [操作系统实验一到实验九合集(哈工大李治军)](https://blog.csdn.net/leoabcd12/article/details/122268321)
- [什么是段寄存器，数据寄存器](https://blog.csdn.net/happylzs2008/article/details/105243075?spm=1001.2101.3001.6650.1&utm_medium=distribute.pc_relevant.none-task-blog-2%7Edefault%7ECTRLIST%7ERate-1-105243075-blog-127131196.pc_relevant_recovery_v2&depth_1-utm_source=distribute.pc_relevant.none-task-blog-2%7Edefault%7ECTRLIST%7ERate-1-105243075-blog-127131196.pc_relevant_recovery_v2&utm_relevant_index=2)
